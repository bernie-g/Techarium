buildscript {
    repositories {
        maven { url = 'https://sizableshrimp.me/maven' }
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        //temporarily use sizableshrimp's FG fork until parchment is out for 1.17
        classpath group: 'me.sizableshrimp.gradle', name: 'ForgeGradle', version: '5.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

version = '1.0'
group = 'software.bernie.techarium'
archivesBaseName = 'techarium'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: "official_snapshot", version: "20210309-1.16.5"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

      runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args '--mod', 'techarium', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }
    }
}
repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://repo.repsy.io/mvn/fadookie/particleman' }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { url "https://maven.tehnut.info" }

    maven {
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { url 'https://maven.blamejared.com' }
    maven {
        // for Immersive Engineering
        url "https://modmaven.dev/"
        content {
            includeGroup 'blusunrize.immersiveengineering'
            includeGroup 'malte0811'
        }
    }
    maven { url = 'https://sizableshrimp.me/maven' }
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.1'

    //Geckolib stuff
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.16.5:3.0.38')

    //adventure
    implementation "net.kyori:adventure-api:$adventure_version"
    implementation "net.kyori:adventure-text-serializer-gson:$adventure_version"
    implementation "net.kyori:adventure-text-serializer-legacy:$adventure_version"
    implementation "net.kyori:adventure-text-serializer-plain:$adventure_version"

    //Integration mods
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    implementation fg.deobf("curse.maven:mekanism-268560:3206392")
    implementation fg.deobf("curse.maven:theoneprobe-245211:3157997")
    implementation fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:$crafttweaker_version")
    implementation fg.deobf("curse.maven:mystical-agriculture-246640:3251398")
    implementation fg.deobf("curse.maven:cucumber-272335:3249370")
    implementation fg.deobf("curse.maven:pams-harvestcraft-2-crops-361385:3276350")
    implementation fg.deobf("curse.maven:thermal-cultivation-271835:3246999")
    implementation fg.deobf("curse.maven:thermal-foundation-222880:3248150")
    implementation fg.deobf("curse.maven:xl-food-mod-plus-448703:3232224")
    implementation fg.deobf("curse.maven:farmers-delight-398521:3310983")
    //compile fg.deobf("curse.maven:immersive-engineering-231951:3233018")
    implementation fg.deobf("com.teamcofh:CoFHCore:1.16.4-1.2.0")
    compileOnly fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:1.16.5-4.2.4-134.7")

    // Compile against the Hwyla API, but run with full mod
    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}:api")
    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${hwyla_version}")

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

shadowJar {
    dependencies {
        include(dependency('net.kyori:.*'))
    }
    relocate 'net.kyori', 'software.bernie.shadowed.net.kyori'

    classifier ''
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "techarium",
            "Specification-Vendor": "techarium",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"techarium",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf{
    shadowJar{}
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}