buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

version = '1.0'
group = 'software.bernie.techarium'
archivesBaseName = 'techarium'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20201028-1.16.3'

      runs {
        client {
            workingDirectory project.file('run')

            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'techarium', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                techarium {
                    source sourceSets.main
                }
            }
        }
    }
}
repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://repo.repsy.io/mvn/fadookie/particleman' }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven { url "https://maven.tehnut.info" }

    maven {
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }
    maven { url 'https://maven.blamejared.com' }
}
dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.1'

    //Geckolib stuff
    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.16.5:3.0.33')

    //adventure
    compile "net.kyori:adventure-api:$adventure_version"
    compile "net.kyori:adventure-text-serializer-gson:$adventure_version"
    compile "net.kyori:adventure-text-serializer-legacy:$adventure_version"
    compile "net.kyori:adventure-text-serializer-plain:$adventure_version"

    //Integration mods
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api")
    compile fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}")
    compile fg.deobf("curse.maven:mekanism-268560:3206392")
    runtimeOnly fg.deobf("curse.maven:theoneprobe-245211:3157997")
    compile fg.deobf("com.blamejared.crafttweaker:CraftTweaker-1.16.5:$crafttweaker_version")
    compile fg.deobf("curse.maven:mystical-agriculture-246640:3251398")
    compile fg.deobf("curse.maven:cucumber-272335:3249370")
    compile fg.deobf("curse.maven:pams-harvestcraft-2-crops-361385:3276350")
    compile fg.deobf("curse.maven:thermal-cultivation-271835:3246999")
    compile fg.deobf("curse.maven:thermal-foundation-222880:3248150")
    compile fg.deobf("curse.maven:cofh-core-69162:3249453")

    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

shadowJar {
    dependencies {
        include(dependency('software.bernie.geckolib3:.*'))
    }

    relocate 'software.bernie.geckolib3', 'software.bernie.shadowed.geckolib'

    classifier ''
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "techarium",
            "Specification-Vendor": "techarium",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"techarium",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf{
    shadowJar{}
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}